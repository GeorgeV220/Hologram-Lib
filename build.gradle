import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.9.22"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group 'org.holoeasy'
version '3.1.1'

apply from: "$rootDir/gradle/publish.gradle"

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:24.0.1"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
        main.kotlin.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/java'
        test.kotlin.srcDirs += 'src/test/kotlin'
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_1_8
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(path: ':core')
}

shadowJar {
    archiveClassifier.set('')
    relocate 'kotlin.', 'org.holoeasy.kotlin.'
    relocate 'org.intellij.lang.annotations', 'org.holoeasy.annotations'
    relocate 'org.jetbrains.annotations', 'org.holoeasy.annotations'
}

tasks.build.dependsOn(shadowJar)
tasks.publishShadowPublicationToMavenLocal.dependsOn(jar, shadowJar)
tasks.publish.dependsOn(shadowJar)